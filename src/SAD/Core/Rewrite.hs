-- |
-- Module      : SAD.Core.Rewrite
-- Copyright   : (c) 2017 - 2018, Steffen Frerix
-- License     : GPL-3
--
-- Term rewriting: extraction of rules and proof of equlities.


{-# LANGUAGE FlexibleContexts #-}
{-# OPTIONS_GHC -fno-warn-incomplete-patterns #-}

module SAD.Core.Rewrite (
  equalityReasoning,
  lpoGe
) where

import Data.List
import Data.Set qualified as Set
import Control.Monad (MonadPlus(..), guard, when, unless)
import Control.Monad.State
import Data.Either
import Control.Monad.Reader
import Data.Text.Lazy (Text)
import Data.Text.Lazy qualified as Text

import SAD.Core.Base
import SAD.Core.Reason
import SAD.Data.Formula
import SAD.Data.Instr
import SAD.Data.Rules (Rule)
import SAD.Data.Text.Context (Context)
import SAD.Helpers (notNull)
import SAD.Export.Representation
import SAD.Core.Message qualified as Message
import SAD.Data.Rules qualified as Rule
import SAD.Data.Text.Block qualified as Block (body, link, position)
import SAD.Data.Text.Context qualified as Context

import Isabelle.Library
import Isabelle.Position qualified as Position
import Isabelle.Bytes (Bytes)


-- Lexicographic path ordering

{- a weighting to parametrize the LPO -}
type Weighting = Bytes -> Bytes -> Bool


{- standard implementation of LPO -}
lpoGe :: Format -> Weighting -> Formula -> Formula -> Bool
lpoGe fmt w t s = twins t s || lpoGt fmt w t s


lpoGt :: Format -> Weighting -> Formula -> Formula -> Bool
lpoGt fmt w tr@Trm {trmName = t, trmArgs = ts} sr@Trm {trmName = s, trmArgs = ss} =
   any (\ti -> lpoGe fmt w ti sr) ts
    || (all (lpoGt fmt w tr) ss
    && ((t == s && lexord (lpoGt fmt w) ts ss)
    || w (represent fmt t) (represent fmt s)))
lpoGt fmt w Trm { trmName = t, trmArgs = ts} v@Var {varName = x} =
  w (represent fmt t) (represent fmt x) || any (\ti -> lpoGe fmt w ti v) ts
lpoGt fmt w v@Var {varName = x} Trm {trmName = t, trmArgs = ts} =
  w (represent fmt x) (represent fmt t) && all (lpoGt fmt w v) ts
lpoGt fmt w Var{varName = x} Var {varName = y} = w (represent fmt x) (represent fmt y)
lpoGt _ _ _ _ = False


lexord :: (Formula -> Formula -> Bool) -> [Formula] -> [Formula] -> Bool
lexord ord (x:xs) (y:ys)
  | ord x y = length xs == length ys
  | otherwise = twins x y && lexord ord xs ys
lexord _ _ _ = False


-- simplification

{- type to record conditions and intermediate steps during simplification -}
type SimpInfo = ([Formula], Text)


{- performs one simplification step. We always try to simplify in a
leftmost-bottommost fashion with respect to the term structure -}
simpstep :: Format -> [Rule] -> Weighting -> Formula -> [(Formula, SimpInfo)]
simpstep fmt rules w = flip runStateT undefined . dive
  where
    dive t@Trm {trmName = tName, trmArgs = tArgs} =
      (do newArgs <- try tArgs; return t {trmArgs = newArgs}) `mplus` applyRule t
    dive v@Var{} = applyRule v

    try [] = mzero
    try (t:ts) = (dive t >>= \nt -> return (nt:ts)) `mplus` fmap (t :) (try ts)

    applyRule t = do
      (f, cnd, rl) <- lift (applyLeftToRight t `mplus` applyRightToLeft t)
      put (cnd, Rule.label rl); return f

    applyLeftToRight = applyRuleDirected True
    applyRightToLeft = applyRuleDirected False

    applyRuleDirected p t = do
      rule <- rules
      let (l,r) =
            if   p
            then (Rule.left rule, Rule.right rule)
            else (Rule.right rule, Rule.left rule)
      sbs <- match l t; let nr = sbs r
      guard $ full nr && lpoGt fmt w (sbs l) nr -- simplified term must be lighter
      return (sbs r, map sbs $ Rule.condition rule, rule)

    full Var {varName = VarHole _} = False; full f = allF full f


{- finds ALL normalforms and their corresponding simplification paths -}
findNormalform :: Format -> [Rule] -> Weighting -> Formula -> [[(Formula, SimpInfo)]]
findNormalform fmt rules w t = map (reverse . (:) (t, trivialSimpInfo)) $ dive t
  where
    trivialSimpInfo = (pure Top, mempty)
    dive t = case simpstep fmt rules w t of
      [] -> return []
      simplifications -> do
        (simplifiedTerm, simpInfo) <- simplifications
        (:) (simplifiedTerm, simpInfo) <$> dive simplifiedTerm


{- finds two matching normalforms and outputs all conditions accumulated
during their rewriting -}
generateConditions ::
  Format -> Position.T -> Bool -> [Rule] -> Weighting -> Formula -> Formula -> VerifyMonad [SimpInfo]
generateConditions fmt pos verbositySetting rules w l r =
  let leftNormalForms  = findNormalform fmt rules w l
      rightNormalForms = findNormalform fmt rules w r
      paths = simpPaths leftNormalForms rightNormalForms
  in  if   null paths
      then log (head leftNormalForms) (head rightNormalForms) >> mzero
      else let (leftPath, rightPath) = head paths
            in showPath leftPath >> showPath rightPath >>
               return (map snd $ leftPath ++ rightPath)
  where
    -- check for matching normalforms and output the paths to them
    simpPaths leftNormalForms rightNormalForms = do
      leftPath@((simplifiedLeft , _):_) <- leftNormalForms
      rightPath@((simplifiedRight, _):_) <- rightNormalForms
      guard (twins simplifiedLeft simplifiedRight)
      return (reverse leftPath, reverse rightPath)

    -- logging and user communication
    log leftNormalForm rightNormalForm = when verbositySetting $ do
      simpLog Message.WRITELN pos "no matching normal forms found"
      showPath leftNormalForm; showPath rightNormalForm
    showPath ((t,_):rest) = when verbositySetting $ do
      simpLog Message.WRITELN pos (represent fmt t)
      mapM_ (simpLog Message.WRITELN pos . format) rest
    -- formatting of paths
    format (t, simpInfo) = " --> " <> make_string (represent fmt t) <> conditions simpInfo
    conditions (conditions, name) =
      (if Text.null name then "" else " by " <> Text.unpack name <> ",") <>
      (if null conditions then "" else " conditions: " <>
        unwords (intersperse "," $ map (make_string . represent fmt) conditions))


{- applies computational reasoning to an equality chain -}
equalityReasoning :: Format -> Position.T -> Context -> VerifyMonad ()
equalityReasoning fmt pos thesis
  | body = whenInstruction printreasonParam $ reasonLog Message.WRITELN pos "equality chain concluded"
  | notNull link = getLinkedRules pos link >>= rewrite fmt pos equation
  | otherwise = rules >>= rewrite fmt pos equation -- if no link is given -> all rules
  where
    equation = strip $ Context.formula thesis
    link = Context.link thesis
    -- body is true for the EC section containing the equlity chain
    body = notNull $ Block.body . head . Context.branch $ thesis


getLinkedRules :: Position.T -> [Text] -> VerifyMonad [Rule]
getLinkedRules pos link = do
  rules <- rules; let setLink = Set.fromList link
  let (linkedRules, unfoundRules) = runState (retrieve setLink rules) setLink
  unless (Set.null unfoundRules) $ warn unfoundRules
  return linkedRules
  where
    warn st =
      simpLog Message.WARNING pos $
        "Could not find rules " <> unwords (map show $ Set.elems st)

    retrieve _ [] = return []
    retrieve s (c:cnt) = let nm = Rule.label c in
      if   Set.member nm s
      then modify (Set.delete nm) >> fmap (c:) (retrieve s cnt)
      else retrieve s cnt


{- fetch all rewrite rules from the global state -}
rules :: VerifyMonad [Rule]
rules = asks rewriteRules


{- applies rewriting to both sides of an equation
and compares the resulting normal forms -}
rewrite :: Format -> Position.T -> Formula -> [Rule] -> VerifyMonad ()
rewrite fmt pos Trm {trmName = TermEquality, trmArgs = [l,r]} rules = do
  verbositySetting <- asks (getInstruction printsimpParam)
  conditions <- generateConditions fmt pos verbositySetting rules (>) l r;
  mapM_ (dischargeConditions fmt pos verbositySetting . fst) conditions
rewrite _ _ _ _ = error "SAD.Core.Rewrite.rewrite: non-equation argument"


{- dischargeConditions accumulated during rewriting -}
dischargeConditions :: Format -> Position.T -> Bool -> [Formula] -> VerifyMonad ()
dischargeConditions fmt pos verbositySetting conditions =
  local setup $ easy >>= hard
  where
    easy = mapM trivialityCheck conditions
    hard hardConditions
      | all isRight hardConditions =
          if all isTop $ rights hardConditions
          then return ()
          else log $ "trivial " <> header rights hardConditions
      | otherwise = do
          log (header lefts hardConditions <> thead (rights hardConditions))
          thesis <- asks currentThesis
          mapM_ (proveThesis' fmt pos . Context.setFormula (wipeLink thesis)) (lefts hardConditions)

    setup state =
      let
        timelimit = SetInt timelimitParam $ getInstruction checktimeParam state
        depthlimit = SetInt depthlimitParam $ getInstruction checkdepthParam state
      in addInstruction timelimit $ addInstruction depthlimit state

    header select conditions = "condition: " <> format (select conditions)
    thead [] = ""; thead conditions = "(trivial: " <> format conditions <> ")"
    format conditions =
      if   null conditions
      then " - "
      else unwords . intersperse "," . map (make_string . represent fmt) $ reverse conditions
    log msg =
      when verbositySetting $ asks currentThesis >>=
        flip (simpLog Message.WRITELN . Block.position . Context.head) msg

    wipeLink thesis =
      let block:restBranch = Context.branch thesis
      in  thesis {Context.branch = block {Block.link = []} : restBranch}
